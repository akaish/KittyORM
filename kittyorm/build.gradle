apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

group

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    productFlavors {
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

group = "net.akaish.kitty.orm"
archivesBaseName = "kitty-orm"
version = "0.2.21"

task deleteJar(type: Delete) {
    delete 'libs/jars/'+archivesBaseName+'.'+version+'.jar'
}

task createJar(type: Copy) {
    from('build/intermediates/bundles/release/')
    into('libs/jars/')
    include('classes.jar')
    rename('classes.jar', archivesBaseName+'.'+version+'.jar')
}

createJar.dependsOn(deleteJar, build)

// Signing
apply plugin: 'signing'
signing {
    sign configurations.archives
}

task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    destinationDir = file("../javadoc/")
    failOnError false
}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from tasks.javadoc.destinationDir
}
//Creating sources with comments
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}
artifacts {
    archives javadocJar
    archives androidSourcesJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: sonatypeUsername, password: sonatypePassword)
            }
            pom.project {
                name "KittyORM"
                packaging 'jar'
                description 'KittyORM - Data Mapper ORM for Android\\SQLite'
                artifactId "kitty-orm"
                url 'https://github.com/akaish/KittyORM'
                scm {
                    connection 'scm:git:git://github.com/akaish/KittyORM.git'
                    developerConnection 'scm:git:ssh://github.com/akaish/KittyORM.git'
                    url 'https://github.com/akaish/KittyORM/tree/master'
                }
                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                        distribution 'repo'
                    }
                }
                developers {
                    developer {
                        id 'akaish'
                        name 'Denis Bogomolov'
                        email 'akaish@mail.ru'
                    }
                }
            }
        }
    }
}
